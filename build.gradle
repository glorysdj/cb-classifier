buildscript {
    ext{
        vertx_version = '3.5.1'
        slf4j_version = '1.7.21'
        logback_version = '1.2.3'
        gson_version = '2.8.4'
        hadoop_version = '2.7.3'
        scala_version = '2.11.12'
        scala_major_version = '2.11'
        spark_version = '2.2.0'
        spark_major_version = '2.2'
        bigdl_version = '0.5.0'
        junit_version = '4.12'
    }

    repositories {
        jcenter()
    }
}

plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.4'
}

group 'cn.azure.chatbot'
version '1.0-SNAPSHOT'

configurations.all {
    resolutionStrategy.force "org.scala-lang:scala-library:$scala_version"
}

apply plugin: 'scala'

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = 'io.vertx.core.Launcher'
def mainVerticleName = 'cn.azure.chatbot.classifier.MainVerticle'

repositories {
    jcenter()
    maven {
        url  "https://dl.bintray.com/windoze/maven"
    }
}

configurations {
    all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
}

dependencies {
    compile "io.vertx:vertx-core:$vertx_version"
    compile "io.vertx:vertx-web:$vertx_version"
    compile "io.vertx:vertx-config:$vertx_version"
    compile "org.slf4j:slf4j-api:$slf4j_version"
    compile "ch.qos.logback:logback-core:$logback_version"
    compile "ch.qos.logback:logback-classic:$logback_version"
    compile "com.google.code.gson:gson:$gson_version"
    compile "org.apache.hadoop:hadoop-aws:$hadoop_version"
    compile group: 'org.apache.spark', name: "spark-mllib_$scala_major_version", version: "$spark_version"
    compile "org.scala-lang:scala-compiler:$scala_version"
    compile "org.scala-lang:scala-reflect:$scala_version"
    compile "org.scala-lang:scala-library:$scala_version"
    compile "com.intel.analytics.bigdl:bigdl-SPARK_$spark_major_version:$bigdl_version"
    compile "com.github.windoze:jfasttext:0.1.0"
    compile "com.huaban:jieba-analysis:1.0.2"
    compile 'org.apache.commons:commons-io:1.3.2'
    testCompile group: 'junit', name: 'junit', version: "$junit_version"
    compile files("$sourceSets.main.scala.outputDir")
}

tasks.compileJava.dependsOn compileScala
tasks.compileScala.dependsOn.remove("compileJava")

shadowJar {
    baseName = 'app'
    classifier = 'shadow'
    zip64 true

    manifest {
        attributes 'Main-Verticle': mainVerticleName
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}
